# SPDX-FileCopyrightText: 2021 â˜­ Emery Hemingway
# SPDX-License-Identifier: Unlicense

import std/[monotimes, os, parseopt, strutils, times]
import tkrzw
import eris

const
  usageMsg = """
Usage: erisdbmerge DESTINATION_DB +SOURCE_DB
Merge ERIS block databases.

The first database file passed on the commandline is
open and the contents of successive database files are
copied into it.

"""

proc usage() =
  quit usageMsg

proc merge(dst, src: DBM; srcPath: string) =
  var
    count1k = 0
    count32k = 0
    countCorrupt = 0
  let start = getMonoTime()
  for key, val in src.pairs:
    block copyBlock:
      if key.len == 32 and val.len in {bs1k.int, bs32k.int}:
        let r = reference val
        for i in 0..31:
          if r.bytes[i] != key[i].byte:
            inc countCorrupt
            break copyBlock
        dst.set(key, val, overwrite = false)
        case val.len
        of 1 shl 10: inc count1k
        of 32 shl 10: inc count32k
        else: discard
      else:
        echo "ignoring record with ", key.len, " byte key and ", val.len, " byte value"
  let
    stop = getMonoTime()
    seconds = inSeconds(stop - start)
  echo srcPath, ": ", count1k, "/", count32k, "/", countCorrupt,
    " blocks copied in ", seconds, " seconds (1KiB/32KiB/corrupt)"

proc rebuild(dbPath: string; dbm: DBM) =
  let rebuildStart = getMonoTime()
  dbm.rebuild()
  let rebuildStop = getMonoTime()
  echo dbPath, " rebuilt in ", inSeconds(rebuildStop - rebuildStart), " seconds"

proc main() =
  var dbPaths: seq[string]

  proc failParam(kind: CmdLineKind, key, val: string) =
    quit "unhandled parameter " & key & " " & val

  for kind, key, val in getopt():
    case kind
    of cmdLongOption:
      case key
      of "help": usage()
      else: failParam(kind, key, val)
    of cmdShortOption:
      case key
      of "h": usage()
      else: failParam(kind, key, val)
    of cmdArgument:
      dbPaths.add key
    of cmdEnd: discard

  if dbPaths.len < 2:
    quit "at least two database files must be specified"
  proc checkPath(path: string) =
    if not fileExists(path):
      quit path & " not found"

  checkPath dbPaths[0]
  var dst = newDbm[HashDBM](dbPaths[0], writeable)
  try:
    for i in 1..dbPaths.high:
      let srcPath = dbPaths[i]
      for j in 0..<i:
        if dbPaths[j] == srcPath:
          quit srcPath & " specified more than once"
            # this might deadlock otherwise
      checkPath srcPath
      var src = newDbm[HashDBM](srcPath, readonly)
      merge(dst, src, srcPath)
      close(src)
    if dst.shouldBeRebuilt:
      rebuild(dbPaths[0], dst)
  finally:
    close(dst)

when isMainModule:
  main()
