# SPDX-FileCopyrightText: 2021 â˜­ Emery Hemingway
# SPDX-License-Identifier: Unlicense

import eris
import std/[hashes, streams, tables]

import asyncdispatch, asyncfutures

type
  ConcatenationStore = ref ConcatenationStoreObj
  ConcatenationStoreObj = object of ErisStoreObj
    index: Table[Reference, int64]
    file: File
    lastSeek: int64
    buf: seq[byte]
    blockSize: BlockSize

proc loadUntil(s: ConcatenationStore; blkRef: Reference; blk: var seq[byte]): bool =
  assert(blk.len == s.blockSize.int)
  s.file.setFilePos(s.lastSeek)
  while not result:
    let n = s.file.readBytes(blk, 0, blk.len)
    if n == 0: return false
    elif n != blk.len:
      raise newException(IOError, "read length mismatch")
    let r = reference(blk)
    s.index[r] = s.lastSeek
    s.lastSeek.inc s.blockSize.int
    result = r == blkRef

method put(s: ConcatenationStore; r: Reference; f: PutFuture) =
  if not s.index.hasKey(r):
    s.buf.setLen(s.blockSize.int)
    if not s.loadUntil(r, s.buf):
      s.file.setFilePos(0, fspEnd)
      let i = s.file.getFilePos
      s.index[r] = i
      let n = s.file.writeBytes(f.mget, 0, f.mget.len)
      if n != f.mget.len:
        raise newException(IOError, "write length mismatch")
  complete f

method get(s: ConcatenationStore; blkRef: Reference): Future[seq[byte]] {.async.} =
  var blk = newSeq[byte](s.blockSize.int)
  if s.index.hasKey blkRef:
    s.file.setFilePos(s.index[blkRef])
    let n = s.file.readBytes(blk, 0, blk.len)
    if n != blk.len:
      raise newException(IOError, "read length mismatch")
  else:
    if not s.loadUntil(blkRef, blk):
      raise newException(ValueError, "ERIS block not found")
  return blk

proc newConcatenationStore*(f: File; bs: BlockSize): ConcatenationStore =
  ## Create a new ``ErisStore`` that concatenates blocks to a ``File``.
  ConcatenationStore(
    file: f,
    lastSeek: f.getFilePos,
    blockSize: bs)

when isMainModule:
  import std/parseopt
  from os import fileExists

  proc usage() =
    stderr.writeLine """
Usage: erissum [OPTION]... FILE [URI]...
Encode or decode a file containing ERIS blocks.

When URIs are supplied then data is read from FILE to stdout,
otherwise data from stdin is written to FILE and a URN is
written to stdout.

  --1k           1KiB block size
  --32k         32KiB block size (default)

If FILE has already been initialized then its block size
will override the requested block size.
"""
    quit QuitFailure

  proc failParam(kind: CmdLineKind, key, val: TaintedString) =
    stderr.writeLine("unhandled parameter ", key, " ", val)
    quit QuitFailure

  func toByte(bs: BlockSize): uint8 =
    case bs
    of bs1k: 0x0a'u8
    of bs32k: 0x0f'u8

  proc checkHeader(f: File; bs: BlockSize): (bool, BlockSize) =
    try:
      const magicStr = "erisx2"
      var magic: array[8, byte]
      f.setFilePos(0)
      let n = f.readBytes(magic, 0, magic.len)
      if n == 0:
        f.write(magicStr)
        discard f.writeBytes([bs.toByte, 0'u8], 0, 2)
        return (true, bs)
      elif n == magic.len:
        if magic[7] != 0'u8: return
        for i in 0..5:
          if magic[i].char != magicStr[i]:
            return
        case magic[6]
        of bs1k.toByte: return (true, bs1k)
        of bs32k.toByte: return (true, bs32k)
        else: discard
    except: discard

  var
    filePath = ""
    blockSize = bs32k
    urns: seq[ErisCap]
  for kind, key, val in getopt():
    case kind
    of cmdLongOption:
      if val != "": failParam(kind, key, val)
      case key
      of "1k": blockSize = bs1k
      of "32k": blockSize = bs32k
      of "help": usage()
      else: failParam(kind, key, val)
    of cmdShortOption:
      if val != "": failParam(kind, key, val)
      case key
      of "h": usage()
      else: failParam(kind, key, val)
    of cmdArgument:
      try: urns.add(parseErisUrn key)
      except:
        if filePath == "":filePath = key
        else: quit("failed to parse ERIS URN " & key)
    of cmdEnd: discard

  if filePath == "":
    quit"A file must be specified"

  let encode = urns.len == 0
  if encode:
    stderr.writeLine "encoding from stdin"
  else:
    stderr.writeLine "decoding to stdout"
  var file: File
  block:
    let mode =
      if encode:
        if fileExists filePath:
          fmReadWriteExisting
        else:
          fmReadWrite
      else: fmRead
    if not open(file, filePath, mode, bs32k.int):
      quit("failed to open " & filePath)
    let (ok, fileBlockSize) = checkHeader(file, blockSize)
    if not ok:
      quit"invalid file header"
    blockSize = fileBlockSize
  var store = newConcatenationStore(file, blockSize)
  if encode:
    try:
      var cap = waitFor store.encode(blockSize, stdin.newFileStream)
      stdout.writeLine($cap)
    except:
      let msg = getCurrentExceptionMsg()
      quit("failed to encode data: " & msg)
  else:
    var buf = newString(bs32k.int)
    for cap in urns:
      try:
        let stream = newErisStream(store, cap)
        while true:
          let n = waitFor stream.readBuffer(buf[0].addr, buf.len)
          if n < buf.len:
            buf.setLen(n)
            stdout.write(buf)
            break
          else: stdout.write(buf)
        close(stream)
      except:
        let msg = getCurrentExceptionMsg()
        quit("failed to decode " & $cap & ": " & msg)
  close(file)
  quit QuitSuccess
